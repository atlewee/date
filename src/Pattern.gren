module Pattern exposing (Pattern, Token(..), fromString)

import Char
import Parser exposing ((|.), (|=), Parser)



-- date formatting pattern


type alias Pattern =
    Array Token


type Token
    = Field Char Int
    | Literal String


fromString : String -> Pattern
fromString str =
    Parser.run (patternHelp []) str
        |> Result.withDefault [ Literal str ]



-- parser


field : Parser Token
field =
    Parser.chompIf Char.isAlpha
        |> Parser.getChompedString
        |> Parser.andThen fieldRepeats


fieldRepeats : String -> Parser Token
fieldRepeats str =
    case String.toArray str of
        [ char ] ->
            Parser.succeed (\x y -> Field char (1 + (y - x)))
                |= Parser.getOffset
                |. Parser.chompWhile ((==) char)
                |= Parser.getOffset

        _ ->
            Parser.problem "expected exactly one char"


escapedQuote : Parser Token
escapedQuote =
    Parser.succeed (Literal "'")
        |. Parser.token "''"


literal : Parser Token
literal =
    Parser.succeed Unit
        |. Parser.chompIf isLiteralChar
        |. Parser.chompWhile isLiteralChar
        |> Parser.getChompedString
        |> Parser.map Literal


isLiteralChar : Char -> Bool
isLiteralChar char =
    char /= '\'' && not (Char.isAlpha char)


quoted : Parser Token
quoted =
    Parser.succeed Literal
        |. Parser.chompIf ((==) '\'')
        |= quotedHelp ""
        |. Parser.oneOf
            [ Parser.chompIf ((==) '\'')
            , Parser.end -- lenient parse for unclosed quotes
            ]


quotedHelp : String -> Parser String
quotedHelp result =
    Parser.oneOf
        [ Parser.succeed Unit
            |. Parser.chompIf ((/=) '\'')
            |. Parser.chompWhile ((/=) '\'')
            |> Parser.getChompedString
            |> Parser.andThen (\str -> quotedHelp (result ++ str))
        , Parser.token "''"
            |> Parser.andThen (\_ -> quotedHelp (result ++ "'"))
        , Parser.succeed result
        ]


patternHelp : Array Token -> Parser (Array Token)
patternHelp tokens =
    Parser.oneOf
        [ Parser.oneOf
            [ field
            , literal
            , escapedQuote
            , quoted
            ]
            |> Parser.andThen (\token -> patternHelp (Array.pushFirst token tokens))
        , Parser.lazy
            (\_ -> Parser.succeed (finalize tokens))
        ]


{-| Reverse list and combine consecutive Literals.
-}
finalize : Array Token -> Array Token
finalize =
    Array.foldl
        (\token tokens ->
            case Array.popFirst tokens of 
                Nothing -> 
                    Array.pushFirst token tokens 

                Just {first,rest} -> 
                    case { token=token,first=first} of 
                        { token = Literal x, first = Literal y } ->
                            Array.pushFirst (Literal (x ++ y)) rest
                        _ -> 
                            Array.pushFirst token tokens
        )
        []
